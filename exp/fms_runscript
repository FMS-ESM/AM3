#!/bin/csh -f
#Minimal runscript
set echo 
#--------------------------------------------------------------------------------------------------------
# define variables
set platform  = intel                                            # A unique identifier for your platform
set npes      = 288                                              # Number of processors
set num_executions = 2                                           # Number of times the model is run. Each run restarts from previous run.
set time_stamp = $cwd/../bin/time_stamp.csh                      # Path to timestamp.csh
set mppnccombine = $cwd/../bin/mppnccombine.$platform            # Path to executable mppnccombine
#set landnccombine = $cwd/../bin/landnccombine.$platform          # Path to executable landnccombine
set landnccombine = /ncrc/home2/fms/local/opt/fre-nctools/20120829/x86_64/bin/combine-ncc # precompiled executable
set fregrid_parallel = $cwd/../tools/fregrid/fregrid_parallel    # Path to executable and grid data for fregrid_parallel
set model_executable = $cwd/exec.$platform/am3.x                 # Path to model executable
set workdir = /lustre/fs/scratch/$USER/work/am3_pubrel # Where model is run and model output is produced
#--------------------------------------------------------------------------------------------------------
source /opt/modules/default/init/csh
module unload PrgEnv-cray PrgEnv-pgi PrgEnv-pathscale PrgEnv-gnu PrgEnv-intel
module unload netcdf
module rm PrgEnv-pgi
module load PrgEnv-intel
module load intel
module load hdf5
module load netcdf
module list
setenv MAIN_PROGRAM coupler_main.o

set namelist   = $cwd/namelists    # path to namelist file (contains all namelists)
set diagtable  = $cwd/diag_table   # path to diagnositics table (specifies fields and files for diagnostic output)
set fieldtable = $cwd/field_table  # path to field table (specifies tracers)
set datatable  = $cwd/data_table   # path to data table (specifies data files used by data_override)
#--------------------------------------------------------------------------------------------------------
# The gaea locations of the input data sets are listed below
set gridSpec  = /lustre/fs/archive/fms/cm3/ipcc_ar5/input/common/C48.C48.tripolar.mosaic.cpio
set initCond  = /lustre/fs/scratch/Peter.Phillipps/archive/initCond.am3p9.tar
set landuse   = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/landuse.nc
set topog     = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/navy_topography.data.nc
set hadisst   = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/hadisst.cpio
set sea_esf   = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/sea_esf_dec2009.cpio
set land_LM3  = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/land_LM3.dec2009.cpio
set aerosols  = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/aerosol_inputs.dec2009.cpio
set cmip5     = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/cmip5_rcp45.cpio
set tropchem  = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/tropchem_nov2009.cpio
set megan2    = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/megan2.1_input.cpio
set emissions = /lustre/fs/scratch/Peter.Phillipps/am3_input_data/emissions.1x1.1859_2010.v2.cpio
#--------------------------------------------------------------------------------------------------------
# A second copy of input files exists here:
#set initCond  = /lustre/ltfs/scratch/Peter.Phillipps/archive/initCond.am3p9.tar
#set landuse   = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/historical/landuse.nc
#set topog     = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/common/navy_topography.data.nc
#set hadisst   = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/common/hadisst.cpio
#set sea_esf   = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/common/sea_esf_dec2009.cpio
#set land_LM3  = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/common/land_LM3.dec2009.cpio
#set aerosols  = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/common/aerosol_inputs.dec2009.cpio
#set cmip5     = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/RCP/cmip5_rcp45.cpio
#set tropchem  = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/common/tropchem_nov2009.cpio
#set megan2    = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/common/megan2.1_input.cpio
#set emissions = /lustre/ltfs/scratch/Larry.Horowitz/archive/cm3/ipcc_ar5/input/historical/emissions.1x1.1859_2010.v2.cpio
#--------------------------------------------------------------------------------------------------------
# The GFDL locations of the input data sets are listed below
#set gridSpec  = /archive/cm3/ipcc_ar5/input/common/C48.C48.tripolar.mosaic.cpio
#set initCond  = /archive/pjp/am3_public_release/initCond.am3p9.tar
#set landuse   = /archive/cm3/ipcc_ar5/input/historical/landuse.nc
#set topog     = /archive/cm3/ipcc_ar5/input/common/navy_topography.data.nc
#set hadisst   = /archive/cm3/ipcc_ar5/input/common/hadisst.cpio
#set sea_esf   = /archive/cm3/ipcc_ar5/input/common/sea_esf_dec2009.cpio
#set land_LM3  = /archive/cm3/ipcc_ar5/input/common/land_LM3.dec2009.cpio
#set aerosols  = /archive/cm3/ipcc_ar5/input/common/aerosol_inputs.dec2009.cpio
#set cmip5     = /archive/cm3/ipcc_ar5/input/RCP/cmip5_rcp45.cpio
#set tropchem  = /archive/cm3/ipcc_ar5/input/common/tropchem_nov2009.cpio
#set megan2    = /archive/cm3/ipcc_ar5/input/common/megan2.1_input.cpio
#set emissions = /archive/cm3/ipcc_ar5/input/historical/emissions.1x1.1859_2010.cpio
#--------------------------------------------------------------------------------------------------------
# The input data also exists as a zipped tarball here: ftp://ftp.gfdl.noaa.gov/perm/AM3_pubrel/AM3_input_data.tar.gz
#--------------------------------------------------------------------------------------------------------
# setup directory structure
if ( -d $workdir ) then
  /bin/rm -rf $workdir/*
else
  mkdir $workdir
endif
cd $workdir
cp /ncrc/home1/Peter.Phillipps/am3_public_release/am3/exp/fms_runscript .
mkdir INPUT RESTART
#--------------------------------------------------------------------------------------------------------
# get input data and executable
cp $namelist   input.nml
cp $diagtable  diag_table
cp $fieldtable field_table
cp $datatable data_table
cp $model_executable .
cd $workdir/INPUT
cpio -iv < $gridSpec
mv ocean_topog.nc topog.nc
tar xvf $initCond
cp $landuse .
cp $topog .
cpio -iv < $hadisst
cpio -iv < $sea_esf
cpio -iv < $land_LM3
cpio -iv < $aerosols
cpio -iv < $cmip5
cpio -iv < $tropchem
cpio -iv < $megan2
cpio -iv < $emissions
cd $workdir

set irun = 1
while ( $irun <= $num_executions )
#--------------------------------------------------------------------------------------------------------
# run the model with aprun
aprun -n $npes ./$model_executable:t
if ($status != 0) then
  echo "Error in execution of $cwd/$model_executable:t"
  exit 1
endif
#--------------------------------------------------------------------------------------------------------
set date_name = `$time_stamp -bf digital`
foreach outfile (logfile.000000.out time_stamp.out diag_integral.out)
  mv $outfile $date_name.$outfile
end

#--------------------------------------------------------------------------------------------------------
# combine diagnostic files, remove the uncombined files, and add the time stamp to the file names
if ( $npes > 1 ) then
  foreach ncfile (`/bin/ls *.nc.0000`)
    $mppnccombine $ncfile:r
    if ($status == 0) then
      rm -f $ncfile:r.[0-9][0-9][0-9][0-9]
      mv $ncfile:r $date_name.$ncfile:r
    else
      echo "Error in execution of $mppnccombine while working on $ncfile:r"
      exit 1
    endif
  end
endif
#--------------------------------------------------------------------------------------------------------
cd $workdir/RESTART
#--------------------------------------------------------------------------------------------------------
# combine land restart files, remove the uncombined files, and add the time stamp to the file names
foreach landname (cana glac lake land snow soil vegn1 vegn2)
  foreach tilenum (1 2 3 4 5 6)
    set distfiles=($landname.res.tile$tilenum.nc.[0-9][0-9][0-9][0-9])
    $landnccombine $distfiles $landname.res.tile$tilenum.nc
    if ($status == 0) then
      rm -f $distfiles
    else
      echo "Error in execution of $landnccombine while working on $landname.res.tile$tilenum.nc"
      exit 1
    endif
  end
end
#--------------------------------------------------------------------------------------------------------
# combine remaining restart files, remove the uncombined files, and add the time stamp to the file names
if ( $npes > 1 ) then
  foreach ncfile (`/bin/ls *.nc.0000`)
    $mppnccombine $ncfile:r
    if ($status == 0) then
      rm -f $ncfile:r.[0-9][0-9][0-9][0-9]
    else
      echo "Error in execution of $mppnccombine while working on $ncfile:r"
      exit 1
    endif
  end
endif
#--------------------------------------------------------------------------------------------------------

#--------------------------------------------------------------------------------------------------------
# Interpolate data to lat-lon grid
cd $workdir
set diagFiles = (`/bin/ls -1 *.tile1.nc | grep -v grid_spec | grep -v horizontal_grid.tile`)
set latlonfiles = ()
cp $fregrid_parallel:h/C48_mosaic.nc .
cp INPUT/horizontal_grid.tile?.nc .
foreach File ($diagFiles)
  set variables = (`ncdump -h $File | grep 'grid_yt, grid_xt' | awk '{print $2}' | cut -d\( -f1`)
  set variables = `echo $variables |sed 's/ /,/g'`
  set basename = $File:r:r
  aprun -n 2 $fregrid_parallel --input_mosaic C48_mosaic.nc --input_file $basename --interp_method conserve_order2 \
                                 --remap_file fregrid_remap_file --nlon 192 --nlat 92 --scalar_field $variables
  if ($status != 0) then
    echo "Error in execution of $fregrid_parallel while working on $basename"
    exit 1
  endif
  set latlonfiles = ($latlonfiles $basename.nc)
end
unset echo
echo 'Fields interpolated to lat-lon grid exist in these files:'
foreach File ($latlonfiles)
  ls -l $cwd/$File
end
set echo
#--------------------------------------------------------------------------------------------------------
# Prepare to run the model again
/bin/rm INPUT/*.res   INPUT/*.res.nc   INPUT/*.res.nc.0???   INPUT/*.res.tile?.nc   INPUT/*.res.tile?.nc.0???
mv    RESTART/*.res RESTART/*.res.nc RESTART/*.res.nc.0??? RESTART/*.res.tile?.nc RESTART/*.res.tile?.nc.0??? INPUT
#--------------------------------------------------------------------------------------------------------
@ irun ++
end
exit 0
